# Copyright (c) 2024 Huawei Device Co., Ltd.libasynctaskexcutorcallback_napi
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/es2panda/es2abc_config.gni")
import("//build/ohos.gni")

es2abc_gen_abc("gen_async_task_callback_abc") {
  src_js = rebase_path("async_task_callback.ts")
  dst_file = rebase_path(target_out_dir + "/async_task_callback.abc")
  in_puts = [ "async_task_callback.ts" ]
  out_puts = [ target_out_dir + "/async_task_callback.abc" ]
  extra_args = [ "--module" ]
}

gen_js_obj("async_task_callback_js") {
  input = "async_task_callback.ts"
  output = target_out_dir + "/async_task_callback.o"
}

gen_js_obj("async_task_callback_abc") {
  input = get_label_info(":gen_async_task_callback_abc", "target_out_dir") +
          "/async_task_callback.abc"
  output = target_out_dir + "/async_task_callback_abc.o"
  dep = ":gen_async_task_callback_abc"
}

ohos_shared_library("asynctaskcallback_napi") {
  sanitize = {
    integer_overflow = true
    ubsan = true
    boundary_sanitize = true
    cfi = true
    cfi_cross_dso = true
    cfi_vcall_icall_only = true
    debug = false
  }
  branch_protector_ret = "pac_ret"

  sources = [ "async_task_callback_module.cpp" ]

  deps = [
    ":async_task_callback_abc",
    ":async_task_callback_js",
  ]

  external_deps = [ "napi:ace_napi" ]

  relative_install_dir = "module/app/appstartup"
  subsystem_name = "ability"
  part_name = "ability_runtime"
}
